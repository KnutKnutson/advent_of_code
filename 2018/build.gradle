plugins {
    // Apply the Kotlin JVM plugin to add support for Kotlin on the JVM.
    id 'org.jetbrains.kotlin.jvm' version '1.3.20'

    // Apply the application plugin to add support for building a CLI application.
    id 'application'
}

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}
sourceCompatibility = 1.11
targetCompatibility = 1.11

sourceSets {
    systemTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs += 'src/system-test/kotlin'
        }
    }
}

configurations {
    systemTestCompile.extendsFrom testCompile
    systemTestRuntime.extendsFrom testRuntime
}

test {
    useJUnitPlatform {}
}

task systemTest(type: Test) {
    useJUnitPlatform {}
    description = 'Runs the system tests.'
    testClassesDirs = sourceSets.systemTest.output.classesDirs
    classpath = sourceSets.systemTest.runtimeClasspath
    shouldRunAfter test
}

dependencies {
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0"
    // Use the Kotlin JDK 8 standard library.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    testCompile "org.assertj:assertj-core:3.11.1"

    testCompile "org.mockito:mockito-core:2.23.4"
    testCompile "org.mockito:mockito-junit-jupiter:2.23.4"
    testCompile 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.3.2'
}

// Define the main class for the application.
mainClassName = 'advent.AppKt'
